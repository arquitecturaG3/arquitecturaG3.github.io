/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:
   
   // La ALU es un circuito digital que proporciona operaciones aritméticas 
   // y lógicas. Es el bloque de construcción fundamental de la unidad central 
   // de procesamiento de una computadora.
   //la ALU la cual básicamente compila todas las compuertas vistas previamente y 
   //expulsa una tabla de verdad en la que se ven todas las compuertas en acción. 
   //Dentro de sus funciones esta: Convertir en cero las entradas, negarlas, sumarlas, 
   //compararlas, incluso convertirlas a valores negativos

   // Zero the x input
    Mux16(a=x, b=false, sel=zx, out=x1);
    Not16(in=x1, out=Nx);
    Mux16(a=x1, b=Nx, sel=nx, out=x2);

    Mux16(a=y, b=false, sel=zy, out=y1);
    Not16(in=y1, out=Ny);
    Mux16(a=y1, b=Ny, sel=ny, out=y2);

    And16(a=x2, b=y2, out=And16);
    Add16(a=x2, b=y2, out=Add16);
    Mux16(a=And16, b=Add16, sel=f, out=z1);

    Not16(in=z1, out=z2);
    Mux16(a=z1, b=z2, sel=no, out=out, out[15]=ng, out[0..7]=zout1, out[8..15]=zout2);

    Or8Way(in=zout1, out=zr1);
    Or8Way(in=zout2, out=zr2);
    Or(a=zr1, b=zr2, out=zr3);
    Not(in=zr3, out=zr);
}
