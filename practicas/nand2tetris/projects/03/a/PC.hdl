/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    // En el Chip PC tenemos 4 posibles casos
    // resetear, sustituir,incrementar,mantener
    // para decidir cual de estas 4 salidas retornamos
    // implementamos una secuencia de Mux16
    // primero decidimos entre mantener o incrementar
    // seguido de decidir entre la salida anterior y sustituir
    // para finalmente decidir entre la salida anterior o resetear
    // Debido al funcionamiento de los Mux16 para implementar
    // las condiciones en el orden indicado, esta son evaluadas
    // desde la ultima hasta la primera, la salida final
    // es almacenada en una Registro de 16 Bitsn y la variable data

    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Put your code here:

    Inc16(in=data, out=dataInc);

    Mux16(a=data, b=dataInc, sel=inc, out=v1);

    Mux16(a=v1, b=in, sel=load, out=v2);
    
    Mux16(a=v2, b=false, sel=reset, out=v3);

    Register(in=v3, load=true, out=data, out=out);
}
